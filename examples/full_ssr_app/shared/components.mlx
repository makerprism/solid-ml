(**
    Shared components for SSR + browser.
*)

type todo = {
  id : int;
  text : string;
  completed : bool;
}

module Make (Env : Solid_ml_template_runtime.Env_intf.TEMPLATE_ENV) = struct
  open Env

  module Html = Env.Html
  module Tpl = Env.Tpl
  module Routes = Routes

  open Html

  (** Keyed list demo (SSR + hydrate adoption) *)
  let keyed_demo () =
    let items, set_items = Signal.create [ "alpha"; "beta"; "gamma" ] in

    let remove_first () =
      match Signal.get items with
      | [] -> ()
      | _ :: rest -> set_items rest
    in

    let reverse () =
      set_items (List.rev (Signal.get items))
    in

    <div>
      <h2>(text "Keyed Hydration Demo")</h2>
      <p>(text "Try reverse/remove; DOM nodes should be adopted and reordered.")</p>

      <div class_="buttons">
        <button class_="btn" onclick=(fun _ -> reverse ())>
          (text "Reverse")
        </button>
        <button class_="btn btn-secondary" onclick=(fun _ -> remove_first ())>
          (text "Remove first")
        </button>
      </div>

      <ul>
        (Tpl.each_keyed
           ~items:(fun () -> Signal.get items)
           ~key:(fun s -> s)
           ~render:(fun s -> <li id=("k-" ^ s)>(text s)</li>))
      </ul>
    </div>

  (** Counter Component *)
  let counter ~initial () =
    let count, set_count = Signal.create initial in

    <div class_="counter-display">
      <h2>(text "Shared Counter")</h2>

      <div>
        (Tpl.text (fun () -> Int.to_string (Signal.get count)))
      </div>

      <div class_="buttons">
        <button class_="btn" onclick=(fun _ -> Signal.update count (fun n -> n - 1))>
          (text "-")
        </button>

        <button class_="btn" onclick=(fun _ -> Signal.update count (fun n -> n + 1))>
          (text "+")
        </button>

        <button class_="btn btn-secondary" onclick=(fun _ -> set_count initial)>
          (text "Reset")
        </button>
      </div>
    </div>

  let counter_content ~initial () =
    Html.fragment [
      counter ~initial ();
      input ~type_:"hidden" ~id:"initial-count" ~value:(string_of_int initial) ();
      <div id="hydration-status" class_="hydration-status">
        (text "Hydrated! Counter is now interactive.")
      </div>;
    ]

  (** Todo List Component *)
  let todo_list ~initial_todos () =
    let todos, _ = Signal.create initial_todos in

    <div class_="todo-list-container">
      <h2>(text "Shared Todo List")</h2>

      <p>
        (Tpl.text (fun () ->
           Signal.get todos
           |> List.filter (fun t -> not t.completed)
           |> List.length
           |> Int.to_string))
        (text " items remaining")
      </p>

      <ul class_="todo-list">
        (Tpl.each_keyed
           ~items:(fun () -> Signal.get todos)
           ~key:(fun todo -> Int.to_string todo.id)
           ~render:(fun todo ->
             <li class_=("todo" ^ if todo.completed then " completed" else "")>
               <span class_="checkbox"></span>
               (text todo.text)
             </li>
           )
        )
      </ul>
    </div>

  let todos_content ~initial_todos () =
    Html.fragment [
      todo_list ~initial_todos ();
      <div id="hydration-status" class_="hydration-status">
        (text "Hydrated! Todos are now interactive.")
      </div>;
    ]

  (** App Layout with Navigation *)
  let app_layout ~current_path ~children () =
    let nav_children =
      Routes.all
      |> List.mapi (fun index route ->
        let href = Routes.path route in
        let class_ =
          if Routes.is_active ~current_path route then "nav-link active" else "nav-link"
        in
        let link = <a href=href class_=class_>(text (Routes.label route))</a> in
        if index = 0 then [ link ] else [ <span>(text " | ")</span>; link ])
      |> List.concat
    in
    <div class_="app-container">
      <div class_="nav">
        (Html.fragment nav_children)
      </div>
      <div class_="content">
        (children)
      </div>
    </div>

  (** Home Page *)
  let home_page () =
    <div>
      <h1>(text "Welcome to Solid ML Isomorphic App")</h1>
      <p>(text "This application runs on both server (SSR) and browser.")</p>
    </div>
end
