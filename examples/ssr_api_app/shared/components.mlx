type user_info = {
  id : int;
  name : string;
  username : string;
  email : string;
  phone : string;
  website : string;
  company : string;
  city : string;
}

type post = {
  id : int;
  user_id : int;
  title : string;
  body : string;
}

type comment = {
  id : int;
  post_id : int;
  name : string;
  email : string;
  body : string;
}

type 'a data_state =
  | Loading
  | Error of string
  | Ready of 'a

type page =
  | Posts_page of post list data_state * user_info list option
  | Users_page of user_info list data_state
  | Post_page of post data_state * comment list data_state
  | User_page of user_info data_state * post list data_state
  | Not_found of string

module App (Env : Solid_ml_template_runtime.Env_intf.TEMPLATE_ENV) = struct
  open Env
  module Html = Env.Html
  module Routes = Routes
  open Html

  let loading_block () =
    <div class_="loading">{Html.text "Loading..."}</div>

  let error_block msg =
    <div class_="error">(Tpl.text_value msg)</div>

  let render_state state ~ready =
    match state with
    | Loading -> loading_block ()
    | Error msg -> error_block msg
    | Ready value -> ready value

  let post_card ?(show_user=true) ?(user_name="") (post : post) =
    let user_label = if user_name = "" then "User #" ^ string_of_int post.user_id else user_name in
    <div class_="card post-card">
      <a href=(Tpl.attr ~name:"href" (fun () -> "/posts/" ^ string_of_int post.id))
         class_="card-link">
        <h3>
          (Tpl.text_value post.title)
        </h3>
        <p>(Tpl.text_value post.body)</p>
      </a>
      (Tpl.nodes (fun () ->
         if show_user then
           <div class_="meta">
             <a href=(Tpl.attr ~name:"href" (fun () -> "/users/" ^ string_of_int post.user_id))
                class_="meta-link">
               (Tpl.text_value user_label)
             </a>
           </div>
         else
           Html.empty))
    </div>

  let user_card (u : user_info) =
    <a href=(Tpl.attr ~name:"href" (fun () -> "/users/" ^ string_of_int u.id))
       class_="card-link">
      <div class_="card user-card">
        <h3>
          (Tpl.text_value u.name)
        </h3>
        <p>(Tpl.text_value ("@" ^ u.username))</p>
        <p>(Tpl.text_value u.email)</p>
        <p>(Tpl.text_value (u.company ^ " | " ^ u.city))</p>
      </div>
    </a>

  let comment_card (c : comment) =
    <div class_="comment">
      <div class_="author">(Tpl.text_value c.name)</div>
      <div class_="email">(Tpl.text_value c.email)</div>
      <div class_="body">(Tpl.text_value c.body)</div>
    </div>

  let posts_page state users_opt =
    <div>
      <div class_="section-title">
        <h2>(Html.text "Recent Posts")</h2>
        <span class_="count">
          (Tpl.nodes (fun () ->
             match state with
             | Ready posts -> Html.text
                 (string_of_int (List.length posts) ^ " posts")
             | Loading -> Html.text "Loading..."
             | Error _ -> Html.text ""))
        </span>
      </div>
      <p>(Html.text "Click on a post to view details and comments, or click on a user to see their profile.")</p>
      <div id="posts-list">
        (Tpl.nodes (fun () ->
           render_state state ~ready:(fun posts ->
             let user_name_for users user_id =
               match List.find_opt (fun (u : user_info) -> u.id = user_id) users with
               | Some u -> u.name
               | None -> ""
             in
             let show_user, name_for_post =
               match users_opt with
               | Some users -> true, (fun post -> user_name_for users post.user_id)
               | None -> false, (fun _ -> "")
             in
             Html.fragment (List.map (fun post ->
               post_card ~show_user ~user_name:(name_for_post post) post) posts))))
      </div>
    </div>

  let users_page state =
    <div>
      <div class_="section-title">
        <h2>(Html.text "All Users")</h2>
        <span class_="count">
          (Tpl.nodes (fun () ->
             match state with
             | Ready users -> Html.text
                 (string_of_int (List.length users) ^ " users")
             | Loading -> Html.text "Loading..."
             | Error _ -> Html.text ""))
        </span>
      </div>
      <p>(Html.text "Click on a user to view their profile and posts.")</p>
      <div id="users-list">
        (Tpl.nodes (fun () ->
           render_state state ~ready:(fun users ->
             Html.fragment (List.map user_card users))))
      </div>
    </div>

  let user_page user_state posts_state =
    <div>
      <div class_="breadcrumb">
        <a href="/">(Html.text "Posts")</a>
        <span class_="separator">(Html.text " / ")</span>
        <a href="/users">(Html.text "Users")</a>
        <span class_="separator">(Html.text " / ")</span>
        <span class_="current">(Html.text "User Profile")</span>
      </div>
      <div class_="section-title">
        <h2>(Html.text "User Profile")</h2>
      </div>
      (Tpl.nodes (fun () ->
         render_state user_state ~ready:(fun (u : user_info) ->
           let initial = String.sub u.name 0 1 in
           <div class_="user-profile">
             <div class_="avatar">(Tpl.text_value initial)</div>
             <div class_="info">
               <h2>(Tpl.text_value u.name)</h2>
               <p class_="username">(Tpl.text_value ("@" ^ u.username))</p>
               <div class_="details">
                 <span>(Tpl.text_value ("Email: " ^ u.email))</span>
                 <span>(Tpl.text_value ("Phone: " ^ u.phone))</span>
                 <span>(Tpl.text_value ("Website: " ^ u.website))</span>
                 <span>(Tpl.text_value ("Company: " ^ u.company))</span>
                 <span>(Tpl.text_value ("City: " ^ u.city))</span>
               </div>
             </div>
           </div>)))
      <div class_="detail-section" id="user-posts">
        <div class_="section-title">
          <h3>(Html.text "Posts")</h3>
          <span class_="count">
            (Tpl.nodes (fun () ->
               match posts_state with
               | Ready posts -> Html.text (string_of_int (List.length posts) ^ " posts")
               | Loading -> Html.text "Loading..."
               | Error _ -> Html.text ""))
          </span>
        </div>
        <div id="posts-list">
          (Tpl.nodes (fun () ->
             render_state posts_state ~ready:(fun posts ->
               Html.fragment (List.map (post_card ~show_user:false) posts))))
        </div>
      </div>
    </div>

  let post_page post_state comments_state =
    <div>
      <div class_="breadcrumb">
        <a href="/">(Html.text "Posts")</a>
        <span class_="separator">(Html.text " / ")</span>
        <span class_="current">(Html.text "Post")</span>
      </div>
      <div class_="detail-section" id="post-detail">
        (Tpl.nodes (fun () ->
           render_state post_state ~ready:(fun post ->
             <div>
               <h2>(Tpl.text_value post.title)</h2>
               <div class_="meta">
                 (Html.text "By ")
                <a href=(Tpl.attr ~name:"href" (fun () -> "/users/" ^ string_of_int post.user_id))>
                  (Tpl.text_value ("User #" ^ string_of_int post.user_id))
                </a>
               </div>
               <div class_="body">(Tpl.text_value post.body)</div>
             </div>)))
      </div>
      <div class_="comments-section" id="comments-section">
        <div class_="section-title">
          <h3>(Html.text "Comments")</h3>
          <span class_="count">
            (Tpl.nodes (fun () ->
               match comments_state with
               | Ready comments -> Html.text (string_of_int (List.length comments) ^ " comments")
               | Loading -> Html.text "Loading..."
               | Error _ -> Html.text ""))
          </span>
        </div>
        <div id="comments-list">
          (Tpl.nodes (fun () ->
             render_state comments_state ~ready:(fun comments ->
               Html.fragment (List.map comment_card comments))))
        </div>
      </div>
    </div>

  let not_found msg =
    <div class_="error">(Tpl.text_value msg)</div>

  let app ~current_path ~page () : Html.node =
    let nav_children =
      Routes.nav_items
      |> List.mapi (fun index route ->
        let href = Routes.path route in
        let class_ =
          if Routes.is_nav_active ~current_path route then "nav-link active" else "nav-link"
        in
        let link =
          Html.a
            ~href
            ~class_
            ~children:[ Html.text (Routes.label route) ]
            ()
        in
        if index = 0 then [ link ] else [ <span class_="nav-divider">(Html.text " ")</span>; link ])
      |> List.concat
    in
    <div id="app">
      <header>
        <h1>{Html.text "solid-ml-server SSR API Demo"}</h1>
        <p class_="subtitle">{Html.text "Server-rendered data with client-side navigation"}</p>
        <nav>
          (Html.fragment nav_children)
        </nav>
      </header>
      <section>
        (match page with
         | Posts_page (posts_state, users_opt) -> posts_page posts_state users_opt
         | Users_page state -> users_page state
         | Post_page (post_state, comments_state) -> post_page post_state comments_state
         | User_page (user_state, posts_state) -> user_page user_state posts_state
         | Not_found msg -> not_found msg)
      </section>
      <div id="hydration-status" class_="hydration-status">
        (Html.text "Hydrated! Client-side navigation enabled.")
      </div>
    </div>
end
